{ config, lib, pkgs, ... }:

with builtins;
with lib;
let cfg = config.modules.shell.zsh;
in {
  options.modules.shell.zsh = with types; {
    enable = mkOption {
      type = bool;
      default = false;
    };

    aliases = mkOption {
      type = (attrsOf (either str path));
      default = {};
    };

    rcInit = mkOption {
      type = lines;
      default = "";
      description = ''
       Zsh lines to be written to $XDG_CONFIG_HOME/zsh/extra.zshrc and sourced by
       $XDG_CONFIG_HOME/zsh/.zshrc
     ''; 
    };

    envInit = mkOption {
      type = lines;
      default = "";
      description = ''
       Zsh lines to be written to $XDG_CONFIG_HOME/zsh/extra.zshenv and sourced
       by $XDG_CONFIG_HOME/zsh/.zshenv
     ''; 
    };

    rcFiles = mkOption {
      type = (listOf (either str path));
      default = []; 
    };

    envFiles = mkOption {
      type = (listOf (either str path));
      default = []; 
    };
  };
  
  config = mkIf cfg.enable {
    users.users.venikx.shell = pkgs.zsh;

    programs.zsh = {
      enable = true;
      enableCompletion = true;
      # I init completion myself, because enableGlobalCompInit initializes it
      # too soon, which means commands initialized later in my config won't get
      # completion, and running compinit twice is slow.
      enableGlobalCompInit = false;
      promptInit = "";
    };

    env = {
      ZDOTDIR     = "$XDG_CONFIG_HOME/zsh";
      ZSH_CACHE   = "$XDG_CACHE_HOME/zsh";
      ZGEN_DIR    = "$XDG_DATA_HOME/zsh";
      ZGEN_SOURCE = "$ZGEN_DIR/zgen.zsh";
    };

    home-manager.users.venikx = {
      home.packages = with pkgs; [
        zsh
        nix-zsh-completions
        bat
        exa
        fd
        fzf
        tldr
      ];

      xdg.configFile."zsh" = {
        source = "/etc/nixos/config/zsh";
        recursive = true; # needed so other modules can write files to it
      };

      xdg.configFile."zsh/extra.zshrc".text =
        let aliasLines = mapAttrsToList (n: v: "alias ${n}=\"${v}\"") cfg.aliases;
        in ''
           # This file was autogenerated, do not edit it!
           ${concatStringsSep "\n" aliasLines}
           ${concatMapStrings (path: "source '${path}'\n") cfg.rcFiles}
           ${cfg.rcInit}
        '';

      xdg.configFile."zsh/extra.zshenv".text = ''
        # This file is autogenerated, do not edit it!
        ${concatMapStrings (path: "source '${path}'\n") cfg.envFiles}
        ${cfg.envInit}
      '';
    };

    system.userActivationScripts.cleanupZgen = "rm -fv $XDG_CACHE_HOME/zsh/*";
  };
}
